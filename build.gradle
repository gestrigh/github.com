plugins {
    id 'java'
    id 'io.qameta.allure' version '2.11.2'
    id "io.freefair.lombok" version "6.0.0-m2"
}

group = 'ru.lamoda'
version = '1.0'

repositories {
    mavenCentral()
}
dependencies {
    testImplementation(
            'org.junit.jupiter:junit-jupiter:5.10.1',
            'com.codeborne:selenide:6.19.1',
            'commons-io:commons-io:2.15.1',
            'com.fasterxml.jackson.core:jackson-core:2.16.1',
            'com.fasterxml.jackson.core:jackson-databind:2.16.1',
            'io.qameta.allure:allure-selenide:2.25.0',
            'org.aeonbits.owner:owner:1.0.12'
    )
}
allure {
    report {
        version.set("2.25.0")
    }
    adapter {
        aspectjWeaver.set(true)
        frameworks {
            junit5 {
                adapterVersion.set("2.25.0")
            }
        }
    }
}
compileJava {
    options.encoding = 'UTF-8'
}
compileTestJava {
    options.encoding = 'UTF-8'
}
tasks.withType(Test) {
    useJUnitPlatform()
    systemProperties(System.getProperties())

    testLogging {
        lifecycle {
            events "started", "skipped", "failed", "standard_error", "standard_out"
            exceptionFormat "short"
        }
    }
}

task withTags(type: Test) {
    String tags = System.getProperty("tags", "smoke")
    useJUnitPlatform {
        systemProperties(System.getProperties())
        includeTags tags.split(',')
    }
}
